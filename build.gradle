buildscript {
    repositories {
        maven {
            name = ":forge"
            url = 'https://files.minecraftforge.net/maven'
        }
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '4.+', changing: true
        classpath "gradle.plugin.net.kyori:blossom:1.1.0"
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'net.minecraftforge.gradle'
apply plugin: "net.kyori.blossom"

version = modVersion
group = modGroup
archivesBaseName = modBaseName

blossom {
    replaceToken '%VERSION%', modVersion
    replaceToken "%CERTIFICATE%", fingerprint
}

sourceCompatibility = targetCompatibility = '1.8'

minecraft {
    mappings channel: 'stable', version: mcp_version
    runs {
        client {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'
        }
        
        server {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'
        }
    }
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft_version

    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"

        expand "version": project.version,
                "mcversion": project.minecraft_version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    options.incremental = true // one flag, and things will get so MUCH faster
    sourceCompatibility = targetCompatibility = '1.8'
}
jar.archiveFileName = "${project.archivesBaseName}-${project.minecraft_version}-${project.version}.jar"

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
}

jar {
    manifest {
        attributes([
                "Maven-Artifact": "${project.group}:${archivesBaseName}:${project.version}",
                "Specification-Title"     : "DKM",
                "Specification-Vendor"    : "ijo42",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.version,
                "Implementation-Vendor"   : "ijo42",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "Git-Commit-Hash"         : getGitHash(),
                "Built-On-Java": "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})",
                "Built-On": "${minecraft_version}-${forge_version}"
        ])
    }
    from 'LICENSE'
}

static def getGitHash() {
    try {
        def process = 'git rev-parse --short HEAD'.execute()
        process.waitFor()
        return (process.exitValue() ? 'unknown' : process.text.trim())
    } catch (IOException ignored) {
        return 'unknown'
    }
}
jar.finalizedBy('reobfJar')
apply from: 'https://gist.github.com/ijo42/6b215e76269a72f37194b924ddda038e/raw/d782226840813ae6703044c51e98b61eb4675875/signjar.groovy'
build.dependsOn signJar

// Hack to include resources for Forge 1.12.2 using FG3
sourceSets { main { output.resourcesDir = output.classesDirs[0] } }
